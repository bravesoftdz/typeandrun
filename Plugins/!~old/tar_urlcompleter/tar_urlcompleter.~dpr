// tar_urlcompleter 1.1 - делает интернет-ссылку полноценной, добавляя http:// в начало
// Copyright © Evgeniy Galanstev 2003-2005

library tar_urlcompleter;

uses Windows, Messages, avl;

type
	TInfo=record
  	size: integer; // Размер структуры
  	plugin: PChar; // Обязательно строка 'Hello! I am the TypeAndRun plugin.' - это распознание плагина
		name: PChar; // Название плагина
		version: PChar; // Его версия
    description: PChar; // Краткое описание функциональности
    author: PChar; // Автор плагина
    copyright: PChar; // Права на плагин
    homepage: PChar; // Домашняя страница плагина
  end;
  TExec=record
  	size: integer; // Размер структуры
    run: integer; // Возвращает, запустилась ли строка
    con_text: PChar; // Если надо отобразить текст в консоли
    con_sel_start: integer; // Начало выделения текста в консоли
    con_sel_length: integer; // Длина выделения
  end;

var
  DllFileName: Array[0..MAX_PATH] of Char; // Путь к dll'шке - нужен для определения папки, где хранить настройки
	Domains: TStringList;
  ConsoleHWND: HWND;

// Выполняется при загрузке dll
// WinHWND - хендл главного окна
procedure Load(WinHWND: HWND); cdecl;
var
	tmp: string;
begin
  ConsoleHWND:=WinHWND;
  GetModuleFileName(HInstance, DllFileName, MAX_PATH); // Вычисляем полный путь к tar_math.dll
  tmp:=DllFileName;
	Domains:=TStringList.Create;
  Domains.LoadFromFile(ExtractFilePath(tmp)+'\domains.txt');
end;
exports Load;

// Выполняется при выгрузке dll
procedure Unload; cdecl;
begin
  Domains.Free;
end;
exports Unload;

// Проверка при принадледность dll к плагинам TypeAndRun
// И возврат информации о плагине
function GetInfo: TInfo; cdecl;
var
	info: TInfo;
begin
	info.size:=SizeOf(TInfo);
	info.plugin:='Hello! I am the TypeAndRun plugin.';
	info.name:='tar_urlcompleter';
	info.version:='1.1';
  info.description:='Complete URL''s running without http://'+#13+'For sample: ghisler.com';
  info.author:='Evgeniy Galanstev (-=GaLaN=-)';
  info.copyright:='Copyright © Evgeniy Galanstev 2003-2005';
  info.homepage:='http://galanc.com/';
	Result:=info;
end;
exports GetInfo;

// Посылает текст окну
// WinHandle - хендл окна
// txtText - посылаемая строка
procedure SendText(WinHandle: HWND; txtText: string);
var
	Data: TCopyDataStruct;
	s: string;
begin
  s:=txtText;
  Data.dwData := 0;
  Data.cbData := Length(s);
  Data.lpData := @s[1];
  SendMessage(WinHandle, WM_COPYDATA, 0, integer(@Data));
end;

// Проверяет соотвествие доменов
function CheckDomains(str: string): boolean;
var
	i: integer;
  tmpD: string;
begin
	Result:=False;
  if Pos(' ', str)<>0 then exit;
  if Pos(':\', str)<>0 then exit;
  for i:=0 to Domains.Count-1 do begin
	  tmpD:=Domains.Strings[i];
  	if tmpD<>'' then begin
      if Pos(tmpD, str)<>0 then begin
        SendText(ConsoleHWND, 'http://'+str);
        Result:=True;
        Break;
      end;
    end;
  end;
end;

// Запуск строки с помощью плагина - возвращает, прошел ли запуск и строку, отображаемую в консоли
// str - запускаемая строка
function RunString(str: PChar): TExec; cdecl;
var
  exec: TExec;
begin
  // Инициализация возвращаемой структуры
  exec.size:=SizeOf(TExec);
	exec.run:=0;
  exec.con_text:='';
  exec.con_sel_start:=0;
  exec.con_sel_length:=0;
	if CheckDomains(str) then exec.run:=1;
  Result:=exec;
end;
exports RunString;

end.
